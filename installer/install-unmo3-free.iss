; OpenMPT Install script for InnoSetup
; Written by Johannes Schultz
; http://openmpt.org/
; http://sagamusix.de/

; This file is provided for creating an install package without the proprietary unmo3.dll (for example for the SourceForge package).
; Instead of including the file in the setup package, the user instead has the possibility to automatically download unmo3.dll from
; our servers.
; The download code requires the ISTool IDE with its downloader extension. You can download it from http://www.istool.org/

#define DOWNLOAD_MO3
#define BaseNameAddition "_sf"
#include "install.iss"

[_ISToolDownload]
Source: http://openmpt.org/files/unmo3/2.4.0.2/unmo3.dll; DestDir: {tmp}; DestName: openmpt-unmo3.dll.tmp; Tasks: downloadmo3
[Code]
// Verify checksum of downloaded file, and if it is OK, copy it to the app directory.
procedure VerifyUNMO3Checksum();
begin
    if(IsTaskSelected('downloadmo3') And FileExists(ExpandConstant('{tmp}\openmpt-unmo3.dll.tmp'))) then
    begin
	    if(GetSHA1OfFile(ExpandConstant('{tmp}\openmpt-unmo3.dll.tmp')) <> 'f8ec44e0a089f17ddd6333956ecc37cd8081e7e8') then
    	begin
    	   MsgBox('Warning: unmo3.dll has been downloaded, but its checksum is wrong! This means that either the downloaded file is corrupted or that a newer version of unmo3.dll is available. The file has thus not been installed. Please obtain unmo3.dll from http://openmpt.org/ and verify its checksum.', mbCriticalError, MB_OK)
    	end else
    	begin
    	   FileCopy(ExpandConstant('{tmp}\openmpt-unmo3.dll.tmp'), ExpandConstant('{app}\unmo3.dll'), true);
    	end;
    	DeleteFile(ExpandConstant('{tmp}\openmpt-unmo3.dll.tmp'));
    end;
end;

// Function generated by ISTool.
function NextButtonClick(CurPage: Integer): Boolean;
begin
	Result := istool_download(CurPage);
end;
